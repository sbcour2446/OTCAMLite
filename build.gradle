import gov.mil.otc._3dvis.buildsystem.JreBuilderTask

plugins {
    id 'application'
    id 'java'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

group 'gov.mil.otc'
version '2.0.2.20'
def liteMode = true

application {
    mainClass.set('gov.mil.otc._3dvis.Main')

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile).tap {
            configureEach {
                options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
            }
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

/*
 * Repositories to use to discover gradle dependencies for 3D Vis
 */
repositories {
    mavenCentral()
}

/*
 * JavaFX dependency configuration
 */
javafx {
    version = "21"
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.graphics', 'javafx.swing']
}

/*
 * What kind of files to treat as resources. In this case, its everything except source related files in the
 * source directories
 */
processResources {
    from(sourceSets.main.java.srcDirs) {
        exclude '**/*.java'
        exclude '**/*.class'
    }
}

/*
 * Dependency assignment and discovery for 3D Vis
 */
dependencies {
    implementation 'org.jetbrains:annotations:23.0.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'

    implementation 'com.google.code.gson:gson:2.10'
    implementation 'org.jogamp.gluegen:gluegen-rt-main:2.3.2'
    implementation 'org.jogamp.jogl:jogl-all-main:2.3.2'
    implementation 'org.xerial:sqlite-jdbc:3.43.0.0'
    implementation 'uk.co.caprica:vlcj:4.8.3'
    implementation 'uk.co.caprica:vlcj-natives:4.8.3'
    implementation 'uk.co.caprica:vlcj-javafx:1.2.0'
    implementation 'net.bramp.ffmpeg:ffmpeg:0.8.0'
    implementation fileTree(dir: 'lib', include: '*.jar')
//    implementation fileTree(dir: 'lib/jogamp', include: '*.jar')
    implementation fileTree(dir: 'lib/tena', include: '*.jar')
    implementation 'org.apache.pdfbox:pdfbox:3.0.0'

}

/*
 * Test library initialization
 */
test {
    useJUnitPlatform()
}

/*
 * Populates the JAR manifest
 */
jar {
    manifest {
        //noinspection GrDeprecatedAPIUsage
        attributes 'Implementation-Title': '3DVis',
                'Implementation-Version': archiveVersion,
                'Main-Class': application.mainClass,
                // If this library has any external jar dependencies, make sure they're listed in the ../libs
                // directory one directory away from where applications will reside
                "Class-Path": configurations.runtimeClasspath.collect { "lib/" + it.getName() }.join(' ')
    }
}

/**
 * Much like a Build.java or R.java file in Android, generate a class to compile that contains pertinent
 * information we need to invoke subprocesses containing our Platform gateways. The file is compiled
 * to avoid the ability of an end user to inject unexpected jars for execution.
 */
tasks.register('BuildInformation') {
    File parentPath = new File(projectDir.absolutePath + "/src/main/java/gov/mil/otc/_3dvis/generated")
    if (parentPath.exists()) {
        parentPath.delete()
    }
    parentPath.mkdirs()
    File outputFile = new File("${parentPath.getAbsolutePath()}/BuildInformation.java")
    outputFile.text =
            """
package gov.mil.otc._3dvis.generated;
/**
 * Auto generated by Gradle.
 * Contains constants representing external versions and names of dependencies for querying at runtime.
 **/
public class BuildInformation {
    /**
     * The 3DVis version.
     */
    public static final String VERSION = "${rootProject.version}" + (${liteMode} ? " (lite)" : "");

    private BuildInformation() {
    }
}
"""
}
build.dependsOn BuildInformation

/*
 * Gathers external dependency JARs and places them in their distribution location
 */
tasks.register('gatherExternalJars', Copy) {
    group = "Deploy 3DVis"
    description = "Deploys a project's external JARs to build\\dist\\lib"

    into layout.buildDirectory.dir("dist\\lib")
    from project.configurations.runtimeClasspath
}

/*
 * Gathers other necessary DLLs and places them in the expected location for distribution
 */
tasks.register('deployExternals', Copy) {
    group = "Deploy 3DVis"
    description = "Copies external dependencies to the expected location for distribution"

    from layout.projectDirectory.dir("external")
    into layout.buildDirectory.dir("dist\\external")
}

/*
 * Deploys the GDAL libraries to their distribution location
 */
tasks.register('deployGDAL', Copy) {
    group = "Deploy 3DVis"
    description = "Copies GDAL dependencies to the expected location for distribution"

    from "${rootProject.projectDir}\\lib\\gdal"
    into layout.buildDirectory.dir("dist\\bin\\gdal")
}

/*
 * Gathers the TENA native binaries to the distribution folder.
 */
tasks.register('deployTenaDLLs', Copy) {
    description = "Gathers the TENA DLLs and copies them to the dist/bin folder for deployment"
    group = 'Deploy 3DVis'

    def dllFilePath = layout.projectDirectory.dir("lib\\tena\\bin")

    if (file(dllFilePath).exists()) {
        // Create a file tree of .dlls
        def dllFileTree = fileTree(dir: dllFilePath, include: '**.dll')

        dllFileTree.files.each { file ->
            into layout.buildDirectory.dir("dist\\bin\\tena")
            from file.absolutePath
        }
    } else {
        throw new GradleException("The location of the TENA native libraries could not be found at ${dllFilePath}")
    }
}

/*
 * Gathers other necessary DLLs and places them in the expected location for distribution
 */
tasks.register('deployVlc', Copy) {
    group = "Deploy 3DVis"
    description = "Copies VLC dependencies to the expected location for distribution"

    from layout.projectDirectory.dir("lib\\vlc")
    into layout.buildDirectory.dir("dist\\bin\\vlc")
}

/*
 * Copies the DIS database to the expected location for distribution
 */
tasks.register('deployMilStd2525', Copy) {
    group = "Deploy 3DVis"
    description = "Copies the milstd2525-symbols.jar to the expected location for distribution"

    from "${rootProject.projectDir}\\milstd2525-symbols.jar"
    into layout.buildDirectory.dir("dist")
}

/*
 * Deploys 3DVis in its expected distribution location
 */
tasks.register('deploy3DVisJar', Copy) {
    group = "Deploy 3DVis"
    description = "Moves 3DVis to its final location for deployment"

    from layout.buildDirectory.dir("libs\\${rootProject.name}-${rootProject.version}.jar")
    into layout.buildDirectory.dir("dist")

    dependsOn build
}

/*
 * Creates and deploys a JRE from our project and external libraries.
 */
tasks.register('deployJRE', JreBuilderTask) {
    distFolderPath = layout.buildDirectory.dir("dist").get().asFile.absolutePath
    group = 'Deploy 3DVis'
    dependsOn gatherExternalJars
}

/*
 * Deploys minimum set of maps and terrain to their expected location.
 */
tasks.register('deployMapsAndTerrain', Copy) {
    group = 'Deploy 3DVis'
    description = "Moves a minimum set of maps and terrain to their expected location"

    from layout.projectDirectory.dir("mapsandterrain")
    into layout.buildDirectory.dir("dist\\mapsandterrain")
}

/*
 * Build custom launcher script for 3DVis
 */
tasks.register('deployLauncherScript') {
    description = "Deploys a launcher script for 3DVis"
    group = 'Deploy 3DVis'
    File file = layout.buildDirectory.file("dist\\launch3DVis.bat").get().asFile
    if (file.exists()) {
        file.delete()
    } else {
        file.getParentFile().mkdirs()
    }

    String text = """
rem The location of the launcher script path. This should be in the base of the root folder containing jre/, app/, libs/ and bin
set "ScriptPath=%~dp0"
set "NativeLibraries=%ScriptPath%\\bin;%ScriptPath%\\bin\\gdal;%ScriptPath%\\bin\\tena;%ScriptPath%\\bin\\vlc"
set "Path=%NativeLibraries%;%ScriptPath%;%Path%"
set "PackagedJavaLocation=%ScriptPath%\\jre\\bin\\javaw"
start "3DVis" "%PackagedJavaLocation%" -DTENA_PLATFORM=${System.getenv("TENA_PLATFORM")} -DTENA_VERSION=${System.getenv("TENA_VERSION")} -jar "%ScriptPath%\\${rootProject.name}-${rootProject.version}.jar" """
    text += (!liteMode ? "fullMode" : "")

    file.text = text
}

/*
 * Deploys everything to create single distribution folder.
 */
tasks.register('deployAll') {
    group = "Deploy 3DVis"

    dependsOn deploy3DVisJar
    dependsOn deployMilStd2525
    dependsOn deployGDAL
    dependsOn deployTenaDLLs
    dependsOn deployVlc
    dependsOn gatherExternalJars
    dependsOn deployExternals
    dependsOn deployJRE
    dependsOn deployMapsAndTerrain
    dependsOn deployLauncherScript
}
